#pragma once

//
// These routines are for solving a class of differential equation, of the form
// ∇²x = b
//
// The x argument in each of these is an initial "guess"
//

inline float4 poisson_step_3d_vector(
    __read_only image3d_t vol_x,
    __read_only image3d_t vol_b,
    int4 vxl_coord,
    float alpha,
    float beta) {

    const sampler_t smp =
        CLK_NORMALIZED_COORDS_FALSE |
        CLK_ADDRESS_CLAMP |
        CLK_FILTER_NEAREST;

    const int4 unit_x = (int4) (1, 0, 0, 0);
    const int4 unit_y = (int4) (0, 1, 0, 0);
    const int4 unit_z = (int4) (0, 0, 1, 0);

    const float3 xk_00 = read_imagef(vol_x, smp, vxl_coord).xyz;

    // +/- z
    const float3 xk_nz = read_imagef(vol_x, smp, (vxl_coord - unit_z)).xyz;
    const float3 xk_pz = read_imagef(vol_x, smp, (vxl_coord + unit_z)).xyz;

    // +/- y
    const float3 xk_ny = read_imagef(vol_x, smp, (vxl_coord - unit_y)).xyz;
    const float3 xk_py = read_imagef(vol_x, smp, (vxl_coord + unit_y)).xyz;

    // +/- x
    const float3 xk_nx = read_imagef(vol_x, smp, (vxl_coord - unit_x)).xyz;
    const float3 xk_px = read_imagef(vol_x, smp, (vxl_coord + unit_x)).xyz;

    const float3 b_ij = read_imagef(vol_b, smp, vxl_coord).xyz;

    const float inv_beta = 1.0f / beta;

    // One can view this as either Jacobi iteration, or iterated averaging [1]
    const float3 jacobi_iterate = (
        xk_nz +
        xk_pz +
        xk_ny +
        xk_py +
        xk_nx +
        xk_px +
        (alpha * b_ij)
    ) * inv_beta;

    const float4 y = (float4) (jacobi_iterate, 0.0f);
    return y;
}


inline float poisson_step_3d_scalar(
    __read_only image3d_t vol_x,
    __read_only image3d_t vol_b,
    int4 vxl_coord,
    float alpha,
    float beta) {

    const sampler_t smp =
        CLK_NORMALIZED_COORDS_FALSE |
        CLK_ADDRESS_CLAMP_TO_EDGE |
        CLK_FILTER_NEAREST;

    const int4 unit_x = (int4) (1, 0, 0, 0);
    const int4 unit_y = (int4) (0, 1, 0, 0);
    const int4 unit_z = (int4) (0, 0, 1, 0);

    const float xk_00 = read_imagef(vol_x, smp, vxl_coord).x;


    // +/- z
    const float xk_nz = read_imagef(vol_x, smp, (vxl_coord - unit_z)).x;
    const float xk_pz = read_imagef(vol_x, smp, (vxl_coord + unit_z)).x;

    // +/- y
    const float xk_ny = read_imagef(vol_x, smp, (vxl_coord - unit_y)).x;
    const float xk_py = read_imagef(vol_x, smp, (vxl_coord + unit_y)).x;

    // +/- x
    const float xk_nx = read_imagef(vol_x, smp, (vxl_coord - unit_x)).x;
    const float xk_px = read_imagef(vol_x, smp, (vxl_coord + unit_x)).x;

    const float b_ij = read_imagef(vol_b, smp, vxl_coord).x;

    const float inv_beta = 1.0f / beta;

    // One can view this as either Jacobi iteration, or iterated averaging [1]
    const float jacobi_iterate = (
        xk_nz +
        xk_pz +
        xk_ny +
        xk_py +
        xk_nx +
        xk_px +
        (alpha * b_ij)
    ) * inv_beta;

    const float y = jacobi_iterate;
    return y;
}